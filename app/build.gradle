plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.app.ocrscanner"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "service"
    productFlavors {
        google {
            dimension "service"
            versionNameSuffix "-google"
        }

        huawei {
            dimension "service"
            versionNameSuffix "-huawei"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

ext {
    huaweiMlKit = '2.0.1.300'
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //DataBinding
    kapt "com.android.databinding:compiler:$gradle_version"

    //Google
    googleImplementation platform('com.google.firebase:firebase-bom:26.5.0')

    //Firebase
    googleImplementation 'com.google.firebase:firebase-analytics-ktx'
    googleImplementation 'com.google.firebase:firebase-crashlytics-ktx'
    googleImplementation 'com.google.android.gms:play-services-mlkit-text-recognition:16.1.3'

    //Huawei
    huaweiImplementation 'com.huawei.agconnect:agconnect-core:1.3.1.300'
    huaweiImplementation "com.huawei.hms:ml-computer-vision-ocr:${huaweiMlKit}"
    huaweiImplementation "com.huawei.hms:ml-computer-vision-ocr-latin-model:${huaweiMlKit}"

    //Utils
    implementation 'com.otaliastudios:cameraview:2.6.2'
}

def taskRequest = getGradle().getStartParameter().getTaskRequests().toString()
if (taskRequest.contains("Google")) {
    println("GOOGLE Type")
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
} else if (taskRequest.contains("Huawei")) {
    println("HUAWEI Type")
    apply plugin: 'com.huawei.agconnect'
}